apply plugin: "maven-publish"

afterEvaluate {

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    publishing {
        repositories {
            maven {
                name = "GithubPackages"
                url = uri(publisher.url)
                credentials {
                    username = github_user
                    password = github_key
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId publisher.groupId
                artifactId publisher.artifactId
                version publisher.version
                artifact bundleReleaseAar
                artifact androidSourcesJar

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.api.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}